@*Echo off

::--------------------------------------------------------------------------------
:: Nothing to do with secondary shells

*If "%_BDEBUGGER" != "1" .AND. ( %_SHELL != 0 .or. %_TRANSIENT != 0 ) *Cancel

::--------------------------------------------------------------------------------
:: Transfer flow to tcExit_first.btm (using call); no sense, but compatible with tcStart_first.btm

*If exist %@Quote[%@Path[%_BatchName]\%@Name[%_BatchName]_first.btm] *Call %@Quote[%@Path[%_BatchName]\%@Name[%_BatchName]_first.btm]

::--------------------------------------------------------------------------------
:: Workaround for bug: http://jpsoft.com/forums/threads/lost-history.5878/
::   If History / DirHistory is local, it is handled by TCC.
::   If History / DirHistory is global, it is handled here
::   $TCC$HistFile and $TCC$DirHistFile are already quoted

*Iff Defined $TCC$HistFile Then
	*History /L
	*Iff IsFile  %[$TCC$HistFile] then
		:: append history, then re-read to remove dups, then save
		*History >> %[$TCC$HistFile]
		*History /F
		*History /R %[$TCC$HistFile]
	*EndIff
	*History > %[$TCC$HistFile]
*EndIff

*Iff Defined $TCC$DirHistFile Then
	*DirHistory /L
	*Iff IsFile  %[$TCC$DirHistFile] then
		:: append DirHistory, then re-read to remove dups, then save
		*DirHistory >> %[$TCC$DirHistFile]
		*DirHistory /F
		*DirHistory /R %[$TCC$DirHistFile]
	*EndIff
	*DirHistory > %[$TCC$DirHistFile]
*EndIff

::--------------------------------------------------------------------------------
:: save CWDs


*Iff Defined $TCC$DirDrivesFile then
	*CDD /a >  %[$TCC$DirDrivesFile]
	*CD     >> %[$TCC$DirDrivesFile]
EndIff

::--------------------------------------------------------------------------------
:: Transfer flow to 4Exit_post.btm (using call)

*If exist %@Quote[%@Path[%_BatchName]\%@Name[%_BatchName]_next.btm] *Call %@Quote[%@Path[%_BatchName]\%@Name[%_BatchName]_next.btm]

:: PAUSE

*Cancel

::===============================================================
:: getPortableIni - get value from pCli.ini, aware on profile settings and specific machine/user INI files

:getPortableIni [_varName _section _name]

	*UnSet %_varName

	*Set _TCC_$profile=%@Quote[%@path[%_cmdspec]..\..]

	*Set _TCC_$value=%@IniRead[%_TCC_$profile\pCli.ini,%_section,%_name]

	*Set _TCC_$iniValue=%@IniRead[%_TCC_$profile\pCli.ini,pCli,ProfileDirectory]
	*If not defined _TCC_$iniValue *Set _TCC_$iniValue=profile
	*Set _TCC_$profile=%@Quote[%@TrueName[%_TCC_$profile\Data\%_TCC_$iniValue]]

	*Do _TCC_$subProfile in /L %USERDOMAIN %USERNAME %COMPUTERNAME

		*Set _TCC_$profile=%@Quote[%@TrueName[%_TCC_$profile\%_TCC_$subProfile]]
		*If not isDir %_TCC_$profile *Leave

		*Set _TCC_$iniValue=%@IniRead[%_TCC_$profile\pCli.ini,%_section,%_name]
		*If defined _TCC_$iniValue *Set _TCC_$value=%_TCC_$iniValue

	*EndDo

	*Set %_varName=%_TCC_$value
	*Return

::===============================================================
::===============================================================
::===============================================================
