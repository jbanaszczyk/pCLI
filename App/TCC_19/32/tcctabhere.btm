@echo off
rem  TCCTabHere.btm
rem  This batch file adds the "TCC tab window here" entry to Windows Explorer

iff %_dosver ge 6.0 then
    echo If you are running Windows 10, 8, 7, 2008, 2012, or Vista, you must first 
    echo start a TCC session as an administrator (right click on the TCC icon and 
    echo select "Run as administrator") and then run TCCTabHere.btm in that session.
endiff

echo.
echo Do you want to add a "TCC tab window here" entry to the Windows Explorer
echos context (right click) menus for directories and drives [Y/N] ?
inkey /k"yn[enter]" %%var

if "%var" ne "y" quit

echo %@regcreate["HKCR\Directory\Shell\TCCTab\command"] > nul
echo %@regcreate["HKCR\Drive\Shell\TCCTab\command"] > nul

iff %@regset["HKCR\Directory\Shell\TCCTab\",REG_SZ,TCC tab window here] != 0 then
    echo Cannot update registry key!
    quit
endiff

echo %@regset["HKCR\Drive\Shell\TCCTab\",REG_SZ,TCC tab window here] > nul

echo %@regset["HKCR\Directory\Shell\TCCTab\command\",REG_SZ,"%@path[%_cmdspec]tcmd.exe" /T "%_cmdspec" /k *cdd "%%L"] > nul
echo %@regset["HKCR\Drive\Shell\TCCTab\command\",REG_SZ,"%@path[%_cmdspec]tcmd.exe" /T "%_cmdspec" /k *cdd "%%L"] > nul

echo.
echo The Windows Explorer context menus have been updated.
unset var
